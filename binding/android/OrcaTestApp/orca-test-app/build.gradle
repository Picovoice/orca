apply plugin: 'com.android.application'


Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }
    if (project.hasProperty("numTestIterations")) {
        properties.put("numTestIterations", project.getProperty("numTestIterations"))
    }
    if (project.hasProperty("procPerformanceThresholdSec")) {
        properties.put("procPerformanceThresholdSec", project.getProperty("procPerformanceThresholdSec"))
    }

    if (project.hasProperty("storePassword")) {
        properties.put("storePassword", project.getProperty("storePassword"))
    }
    if (project.hasProperty("storeFile")) {
        properties.put("storeFile", project.getProperty("storeFile"))
    }
    if (project.hasProperty("keyAlias")) {
        properties.put("keyAlias", project.getProperty("keyAlias"))
    }
    if (project.hasProperty("keyPassword")) {
        properties.put("keyPassword", project.getProperty("keyPassword"))
    }
}

android {
    compileSdk defaultTargetSdkVersion

    defaultConfig {
        applicationId "ai.picovoice.orca.testapp"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
        resValue 'string', 'numTestIterations', properties.getProperty("numTestIterations", "")
        resValue 'string', 'procPerformanceThresholdSec', properties.getProperty("procPerformanceThresholdSec", "")
    }

    signingConfigs {
        release {
            storePassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile", ".dummy.jks"))
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    if (System.getProperty("testBuildType", "debug") == "integ") {
        testBuildType("release")
    }

    tasks.register('copyParams', Copy) {
        from("$projectDir/../../../../lib/common/")
        into("$projectDir/src/main/assets/models")
    }

    sourceSets {
        androidTest {
            java {
                if (System.getProperty("testBuildType", "debug") == "perf") {
                    exclude "**/StandardTests.java"
                    exclude "**/AlignmentTests.java"
                    exclude "**/SentenceTests.java"
                    exclude "**/IntegrationTest.java"
                } else if (System.getProperty("testBuildType", "debug") == "integ") {
                    exclude "**/StandardTests.java"
                    exclude "**/AlignmentTests.java"
                    exclude "**/SentenceTests.java"
                    exclude "**/PerformanceTest.java"
                } else {
                    exclude "**/IntegrationTest.java"
                    exclude "**/PerformanceTest.java"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
    }
    namespace 'ai.picovoice.orca.testapp'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.google.errorprone:error_prone_annotations:2.36.0'
    implementation 'ai.picovoice:orca-android:1.1.1'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.5.1')
    androidTestImplementation('ai.picovoice:orca-android:1.0.0')
}

afterEvaluate {
    tasks."mergeDebugAssets".dependsOn "copyParams"
    tasks."mergeReleaseAssets".dependsOn "copyParams"
    tasks."generateDebugLintReportModel".dependsOn "copyParams"
    tasks."generateReleaseLintVitalReportModel".dependsOn "copyParams"
    tasks."lintAnalyzeDebug".dependsOn "copyParams"
    tasks."lintVitalAnalyzeRelease".dependsOn "copyParams"
}
